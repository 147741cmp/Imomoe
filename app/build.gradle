plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'AndResGuard'
    id 'com.efs.sdk.plugin'
}

apply from: secret
apply from: andresguard

android.defaultConfig {
    secret.buildConfigField.forEach({ k, v ->
        buildConfigField("String", k, "\"${v}\"")
    })
    secret.shieldTextList.forEach({ k, v ->
        buildConfigField("String[]", k, v)
    })
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.skyd.imomoe"
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 15
        versionName "1.1.3_beta2"
        flavorDimensions "versionName"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        renderscriptTargetApi 19
        renderscriptSupportModeEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        ndk {
            abiFilters 'armeabi'
        }

        manifestPlaceholders = secret.manifestPlaceholders
    }

    signingConfigs {
        release {
            keyAlias secret.sign.RELEASE_KEY_ALIAS
            keyPassword secret.sign.RELEASE_KEY_PASSWORD
            storeFile file('../key.jks')
            storePassword secret.sign.RELEASE_STORE_PASSWORD
        }
    }

    productFlavors {
        Github {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "Github"]
        }
        Gitee {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "Gitee"]
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "app_release_${variant.productFlavors[0].name.toLowerCase()}_v${variant.versionName}.apk"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false       // 使用换肤功能则必须设置为false，否则启动崩溃
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            applicationIdSuffix '.debug' // 一台手机debug release共存
            ndk {
                abiFilters 'armeabi', 'x86', 'x86_64'
            }
        }
        release {
            signingConfig signingConfigs.release    //签名
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources false       // 使用换肤功能则必须设置为false，否则启动崩溃
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            ndk {
                abiFilters 'armeabi'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    packagingOptions {
        exclude 'META-INF/beans.xml'
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/README.md'
        exclude 'META-INF/CHANGES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/eclipse.inf'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/nanohttpd/**'
        exclude 'about_files/LICENSE_CDDL.txt'
        exclude 'DebugProbesKt.bin'
        exclude 'jetty-dir.css'
        exclude 'plugin.properties'
        exclude 'org/seamless/**'
        exclude 'org/eclipse/jetty/**'
        exclude 'org/fourthline/cling/**'
        exclude 'okhttp3/internal/**'
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation "androidx.viewpager2:viewpager2:1.0.0"
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    implementation 'com.github.bumptech.glide:okhttp3-integration:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'
    implementation "com.squareup.okhttp3:okhttp:4.9.0"
    implementation 'com.shuyu:GSYVideoPlayer:8.1.0'
    implementation 'com.squareup.retrofit2:retrofit:2.7.2'
    implementation 'com.squareup.retrofit2:converter-gson:2.7.2'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.7.2'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    implementation 'io.reactivex.rxjava3:rxjava:3.0.0'
    implementation 'com.hjq:xxpermissions:10.2'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.4.1'
    implementation 'com.afollestad.material-dialogs:core:3.3.0'
    implementation 'com.afollestad.material-dialogs:input:3.3.0'
    implementation "androidx.room:room-runtime:2.2.5"
    implementation "androidx.room:room-ktx:2.2.5"
    kapt "androidx.room:room-compiler:2.2.5"
    androidTestImplementation "androidx.room:room-testing:2.2.5"
    implementation 'com.liulishuo.filedownloader:library:1.7.7'
    //cling 2.1.2版本无法搜索到设备，不要升级
    implementation 'org.fourthline.cling:cling-core:2.1.1'
    implementation 'org.fourthline.cling:cling-support:2.1.1'
    implementation 'org.eclipse.jetty:jetty-server:8.1.22.v20160922'
    implementation 'org.eclipse.jetty:jetty-servlet:8.1.22.v20160922'
    implementation 'org.eclipse.jetty:jetty-client:8.1.22.v20160922'
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.4'
    implementation 'org.greenrobot:eventbus:3.2.0'
    implementation "com.umeng.umsdk:common:9.4.0"
    implementation "com.umeng.umsdk:asms:1.2.3" // asms包依赖
    implementation 'com.umeng.umsdk:apm:1.4.0'  // U-APM产品包依赖
    implementation 'com.scwang.smart:refresh-layout-kernel:2.0.3'      //核心必须依赖
    implementation 'com.scwang.smart:refresh-header-material:2.0.3'    //谷歌刷新头
    implementation 'com.scwang.smart:refresh-footer-ball:2.0.3'        //球脉冲加载
    implementation 'com.github.ctiao:DanmakuFlameMaster:0.9.25'
    implementation project(':skin')
    implementation project(':skin_blue')
    implementation project(':skin_dark')
}

// UM U-APM性能报告
efs {
    //是否对启动过程进程插桩的开关，如果使用自动集成监控则必须开启
    isAutoTrack = true
    //您自定义Application的类名称，必填项，如没有自定义则填写系统Application
    applicationName = "App"
    //您自定义Activity的类名称，必填项，将您所有Activity的类名按如下格式填写
    activityList = [
            "AboutActivity",
            "AnimeDetailActivity",
            "AnimeDownloadActivity",
            "BaseActivity",
            "ClassifyActivity",
            "CrashActivity",
            "DlnaActivity",
            "DlnaControlActivity",
            "FavoriteActivity",
            "HistoryActivity",
            "LicenseActivity",
            "MainActivity",
            "MonthAnimeActivity",
            "PlayActivity",
            "RankActivity",
            "SearchActivity",
            "SettingActivity",
            "SimplePlayActivity",
            "DetailPlayerActivity"
    ]
}

//AndResGuard资源混淆工具
andResGuard {
    // 使用mappingFile，防止需要换肤的资源id被替换
//    mappingFile = file("./resource_mapping.txt")
    mappingFile = null
    use7zip = true
    useSign = true
    // 打开这个开关，会keep住所有资源的原始路径，只混淆资源的名字
    keepRoot = false
    // 设置这个值，会把arsc name列混淆成相同的名字，减少string常量池的大小
    fixedResName = "arg"
    // 打开这个开关会合并所有哈希值相同的资源，但请不要过度依赖这个功能去除去冗余资源
    mergeDuplicatedRes = true
    whiteList = andresguard.whiteList
    compressFilePattern = [
            "*.png",
            "*.jpg",
            "*.jpeg",
            "*.gif",
            "resources.arsc"
    ]
    sevenzip {
        artifact = 'com.tencent.mm:SevenZip:1.2.20'
        //path = "/usr/local/bin/7za"
    }

    /**
     * 可选： 如果不设置则会默认覆盖assemble输出的apk
     **/
    // finalApkBackupPath = "${project.rootDir}/final.apk"

    /**
     * 可选: 指定v1签名时生成jar文件的摘要算法
     * 默认值为“SHA-1”
     **/
    // digestalg = "SHA-256"
}

//class转jar
//删除之前打出的包，默认将包打在'build/libs/'下
task deleteOldJar(type: Delete) {
    delete 'build/libs/CustomDataSource.jar'
}
//打包操作
task makeJar(type: Jar) {
    //要打成的包的名字
    baseName 'CustomDataSource'
    //选取要打包的文件夹
    from('build\\tmp\\kotlin-classes\\GiteeDebug\\com\\skyd\\imomoe\\model\\impls')
    //需要跟实际类的包名路径一样
    into('com/skyd/imomoe/model/impls')
    //排除在外的文件
    exclude('BuildConfig.class', 'R.class', 'MainActivity.class')
    //排除以R$开头的文件
    exclude { it.name.startsWith('R$') }
}
//打包~
makeJar.dependsOn(deleteOldJar)